apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: 'com.google.gms.google-services'
apply plugin: "com.facebook.react"

import com.android.build.OutputFile
import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os

def getPassword(String currentUser, String keyChain) {
   def stdout = new ByteArrayOutputStream()
   def stderr = new ByteArrayOutputStream()
   exec {
       commandLine 'security', '-q', 'find-generic-password', '-a', currentUser, '-s', keyChain, '-w'
       standardOutput = stdout
       errorOutput = stderr
       ignoreExitValue true
   }
   //noinspection GroovyAssignabilityCheck
      stdout.toString().trim()
}

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore.
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US.  Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'org.webkit:android-jsc:+'

/**
 * Architectures to build native code for.
 */

def reactNativeArchitectures() {
    def value = project.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

// Load versions
def packageSlurper = new JsonSlurper()
def packageJson = packageSlurper.parse file('../../package.json')
def packageVersion = packageJson.version
def versionCodeValue = Integer.parseInt(file('../../VERSION_CODE').text)

// Read keys.json file for credentials
def keysJson = packageSlurper.parse file('../../assets/keys.json')

def username = System.properties['user.name']
def keystorePass = project.hasProperty('TONHUB_UPLOAD_STORE_PASSWORD') ? getPassword(username, TONHUB_UPLOAD_STORE_PASSWORD): null

android {
    ndkVersion rootProject.ext.ndkVersion

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        prefab true
    }

    defaultConfig {
        applicationId 'com.tonhub.wallet'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCodeValue
        versionName packageVersion
        
        // Intercom configuration
        buildConfigField 'String', 'INTERCOM_ANDROID_API', "\"${keysJson.INTERCOM_ANDROID_API}\""
        buildConfigField 'String', 'INTERCOM_APP', "\"${keysJson.INTERCOM_APP}\""
        buildFeatures {
            buildConfig = true
        }
        externalNativeBuild {
            cmake {
                arguments(
                    "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON",
                    "-DCMAKE_SHARED_LINKER_FLAGS=-Wl,-z,max-page-size=16384",
                    "-DCMAKE_EXE_LINKER_FLAGS=-Wl,-z,max-page-size=16384"
                )
            }
        }
    }

    flavorDimensions "version"

    productFlavors {
        netmain {
            applicationId 'com.tonhub.wallet'
        }
        nettest {
            applicationId 'com.tonhub.wallet.testnet'
        }
    }

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include (*reactNativeArchitectures())
        }
    }
    
    // Support for 16KB page sizes on older AGP versions
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += ['**/libc++_shared.so', '**/libfbjni.so']
        }
    }
    
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
	    if (project.hasProperty('TONHUB_UPLOAD_STORE_FILE')) {
                storeFile file(TONHUB_UPLOAD_STORE_FILE)
                storePassword keystorePass
                keyAlias TONHUB_UPLOAD_KEY_ALIAS
                keyPassword keystorePass
	    }
	}
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // https://developer.android.com/studio/build/configure-apk-splits.html
            def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }

        }
    }

    namespace 'com.tonhub.wallet'
}

dependencies {
    implementation "com.google.android.gms:play-services-ads-identifier:18.0.1"
    implementation "com.android.installreferrer:installreferrer:2.2"

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")
    implementation 'com.facebook.fbjni:fbjni:0.3.0'

    def isGifEnabled = (findProperty('expo.gif.enabled') ?: "") == "true";
    def isWebpEnabled = (findProperty('expo.webp.enabled') ?: "") == "true";
    def isWebpAnimatedEnabled = (findProperty('expo.webp.animated') ?: "") == "true";

    // All fresco packages should use the same version
    // Fresco 3.4.0+ supports 16KB page sizes required for Android 15+
    if (isGifEnabled || isWebpEnabled) {
        implementation 'com.facebook.fresco:fresco:3.5.0'
        implementation 'com.facebook.fresco:imagepipeline-okhttp3:3.5.0'
    }

    if (isGifEnabled) {
        // For animated gif support
        implementation 'com.facebook.fresco:animated-gif:3.5.0'
    }

    if (isWebpEnabled) {
        // For webp support
        implementation 'com.facebook.fresco:webpsupport:3.5.0'
        if (isWebpAnimatedEnabled) {
            // Animated webp support
            implementation 'com.facebook.fresco:animated-webp:3.5.0'
        }
    }
    
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:1.0.0")
    implementation 'androidx.biometric:biometric:1.2.0-alpha05'

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'com.google.firebase:firebase-installations:17.0.0'
    implementation 'com.google.firebase:firebase-iid:21.1.0'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'androidx.work:work-runtime-ktx:2.7.0'
    implementation 'com.google.android.gms:play-services-tapandpay:+'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'cloud.mindbox:mindbox-firebase'
}

// RN 0.76+ doesn't use native_modules.gradle

configurations.all {
   resolutionStrategy {
      // Keep your existing browser force
      force 'androidx.browser:browser:1.8.0'
      // force an older version
       force 'io.intercom.android:intercom-sdk-base:14.0.1'
           // align fragment to a stable version to avoid dexing issues
           force 'androidx.fragment:fragment:1.6.2'
      // Force Fresco 3.5.0 for 16KB page size alignment (Android 15+)
      force 'com.facebook.fresco:fresco:3.5.0'
      force 'com.facebook.fresco:imagepipeline:3.5.0'
      force 'com.facebook.fresco:imagepipeline-base:3.5.0'
      force 'com.facebook.fresco:nativeimagefilters:3.5.0'
      force 'com.facebook.fresco:nativeimagetranscoder:3.5.0'
      force 'com.facebook.fresco:animated-gif:3.5.0'
      force 'com.facebook.fresco:webpsupport:3.5.0'
      force 'com.facebook.fresco:animated-webp:3.5.0'
      force 'com.facebook.fresco:animated-base:3.5.0'
      force 'com.facebook.fresco:ui-common:3.5.0'
      force 'com.facebook.fresco:drawee:3.5.0'
      force 'com.facebook.fresco:fbcore:3.5.0'
   }
}
